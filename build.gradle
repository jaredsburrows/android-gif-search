buildscript {
    rootProject.ext.ci = rootProject.hasProperty("ci")

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle:build-scan-plugin:1.0"
        classpath "com.android.tools.build:gradle:2.2.0-rc2"
        classpath "com.novoda:gradle-android-command-plugin:1.6.0"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.2.0"
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
        classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
        classpath "me.tatarka:gradle-retrolambda:3.3.0"
        classpath "me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2"
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://jitpack.io" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply plugin: "com.android.application"
apply plugin: "android-command"
apply plugin: "com.github.triplet.play"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "com.neenbedankt.android-apt"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/buildScan.gradle")
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    defaultConfig {
        applicationId "burrows.apps.giphy.example"
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.txt"
            signingConfig signingConfigs.debug
        }
    }

    packagingOptions {
        exclude "META-INF/rxjava.properties"
    }
}

// Retrolambda - exclude the version that the android plugin depends on
configurations {
    classpath {
        exclude group: "com.android.tools.external.lombok"
    }

    androidTestCompile {
        androidTestCompile {
            exclude group: "com.android.support"
        }
    }
}

def supportLibraryVersion = "24.2.1"
def butterKnifeVersion = "8.4.0"
def okHttpVersion = "3.4.1"
def retrofitVersion = "2.1.0"
def daggerVersion = "2.7"
def espressoVersion = "2.2.2"

dependencies {
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0-RC2"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "io.reactivex.rxjava2:rxandroid:2.0.0-RC1"
    compile "io.reactivex.rxjava2:rxjava:2.0.0-RC2"
    compile "pl.droidsonroids.gif:android-gif-drawable:1.2.0"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
    compile "com.google.dagger:dagger:${daggerVersion}"

    debugCompile "com.squareup.leakcanary:leakcanary-android:1.4"
    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.4"

    apt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    apt "com.google.dagger:dagger-compiler:${daggerVersion}"

    provided "org.glassfish:javax.annotation:10.0-b28"

    androidTestCompile "junit:junit:4.12"
    androidTestCompile "org.mockito:mockito-core:1.9.5"
    androidTestCompile "com.squareup.assertj:assertj-android:1.1.1"
    androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${espressoVersion}"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${espressoVersion}") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }
    androidTestCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.4"

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "com.squareup.assertj:assertj-android:1.1.1"
    testCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    testCompile "com.squareup.retrofit2:retrofit-mock:${retrofitVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"
    testCompile "org.robolectric:shadows-support-v4:3.1.2"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:1.4"

    retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:2.3.0"
}
