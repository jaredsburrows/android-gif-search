buildscript {
  apply from: rootProject.file("gradle/versions.gradle")

  rootProject.ext.ci = rootProject.hasProperty("ci")

  repositories {
    google()
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath plugs.gradle
    classpath plugs.kotlinGradlePlugin
    classpath plugs.gradleAndroidCommandPlugin
    classpath plugs.playPublisher
    classpath plugs.buildScanPlugin
    classpath plugs.dexcountGradlePlugin
    classpath plugs.gradleAndroidApkSizePlugin
    classpath plugs.coverallsGradlePlugin
    classpath plugs.gradleErrorpronePlugin
    classpath plugs.gradleVersionsPlugin
    classpath plugs.gradleLicensePlugin
  }
}

repositories {
  google()
  jcenter()
}

apply from: rootProject.file("gradle/scan.gradle")
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "android-command"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/quality.gradle")
apply from: rootProject.file("gradle/publish.gradle")
apply from: rootProject.file("gradle/wrapper.gradle")
apply plugin: "com.github.triplet.play"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.jaredsburrows.license"

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    applicationId "burrows.apps.example.gif"
    versionCode 1
    versionName "1.0"
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    testApplicationId "burrows.apps.example.gif.test"
    testInstrumentationRunner "test.CustomTestRunner"
    resConfigs "en" // Keep only english resource files for now
    vectorDrawables.useSupportLibrary = true
  }

  dataBinding {
    enabled = true
    enabledForTests = true
  }

  compileOptions {
    sourceCompatibility rootProject.ext.sourceCompatibilityVersion
    targetCompatibility rootProject.ext.sourceCompatibilityVersion
  }

  dexOptions {
    preDexLibraries !rootProject.ext.ci
  }

  // Shouldn"t the plugin be doing this for us?
  sourceSets {
    androidTest.java.srcDirs += "src/androidTest/kotlin"
    main.java.srcDirs += "src/main/kotlin"
    test.java.srcDirs += "src/test/kotlin"
  }

  lintOptions {
    textReport true
    textOutput "stdout"
    checkAllWarnings true
    warningsAsErrors true
    disable "UnusedResources"              // Unused will be removed on release
    disable "IconExpectedSize"             // Using the material icons provided from Google
    disable "GoogleAppIndexingApiWarning"  // We might want to index our app later
    disable "InvalidPackage"               // Butterknife, Okio and Realm
    disable "ResourceType"                 // Annotation binding
    disable "GradleDependency"
    disable "NewerVersionAvailable"
  }

  // Debug - testing and letting others use
  signingConfigs {
    debug {
      storeFile rootProject.file(".buildscript/debug.keystore")
      storePassword rootProject.ext.debugKeystorePass
      keyAlias rootProject.ext.debugKeystoreUser
      keyPassword rootProject.ext.debugKeystorePass
    }
  }

  buildTypes {
    debug {
      debuggable true
      if (rootProject.ext.ci) testCoverageEnabled true // generated coverage for android tests
      applicationIdSuffix ".debug"
    }

    release {
      minifyEnabled true
      shrinkResources true
      proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
      proguardFile rootProject.file("config/proguard/proguard-rules.txt")
      signingConfig signingConfigs.debug
    }
  }

  testOptions {
    animationsDisabled = true

    unitTests {
      returnDefaultValues = true

      all {
        jacoco {
          includeNoLocationClasses = true
        }
      }
    }
  }

  packagingOptions {
    exclude "**/*.txt"
    exclude "**/*.xml"
    exclude "**/*.properties"
  }
}

// Resolves dependency versions across test and production APKs, specifically, transitive
// dependencies. This is required since Espresso internally has a dependency on support-annotations.
configurations.all {
  resolutionStrategy.force "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
}

dependencies {
  // Android/Google
  compile deps.design
  compile deps.cardviewv7
  // Other
  compile deps.kotlinStdlib
  compile deps.okhttp
  compile deps.loggingInterceptor
  compile deps.adapterRxjava2
  compile deps.converterGson
  compile deps.retrofit
  compile deps.rxAndroid
  compile deps.rxJava
  compile deps.glide
  compile deps.okhttp3Integration
  compile deps.dagger

  kapt deps.daggerCompiler
  kapt deps.compiler

  debugCompile deps.leakcanaryAndroid
  releaseCompile deps.leakcanaryAndroidNoOp

  androidTestCompile deps.junit
  androidTestCompile deps.assertjCore
  androidTestCompile deps.dexmaker
  androidTestCompile deps.dexmakerMockito
  androidTestCompile deps.runner
  androidTestCompile deps.espressoCore
  androidTestCompile deps.espressoIntents
  androidTestCompile(deps.espressoContrib) { exclude group: "com.android.support" }
  androidTestCompile deps.mockwebserver
  androidTestCompile deps.dagger

  kaptAndroidTest deps.daggerCompiler

  testCompile deps.junit
  testCompile deps.assertjCore
  testCompile deps.mockitoCore
  testCompile deps.leakcanaryAndroidNoOp
  testCompile deps.mockwebserver
  testCompile deps.equalsverifier
}
