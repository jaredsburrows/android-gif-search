buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:2.1.3"
        classpath "com.novoda:gradle-android-command-plugin:1.6.0"
        classpath "com.github.triplet.gradle:play-publisher:1.1.4"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.5.6"
        classpath "com.vanniktech:gradle-android-apk-size-plugin:0.2.0"
        classpath "me.tatarka:gradle-retrolambda:3.2.5"
        classpath "me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2"
        classpath "com.neenbedankt.gradle.plugins:android-apt:1.8"
    }
}

plugins {
    id "com.gradle.build-scan" version "1.0"
    id "net.ltgt.errorprone" version "0.0.8"
    id "com.github.kt3k.coveralls" version "2.6.3"
    id "com.github.ben-manes.versions" version "0.13.0"
    id "build-dashboard"
    id "pmd"
    id "jacoco"
    id "findbugs"
    id "checkstyle"
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://jitpack.io" }
}

apply plugin: "com.android.application"
apply plugin: "android-command"
apply plugin: "com.github.triplet.play"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "com.neenbedankt.android-apt"
apply from: rootProject.file("gradle/buildScan.gradle")
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    defaultConfig {
        applicationId "burrows.apps.giphy.example"
        versionCode 1
        versionName "1.0"

        vectorDrawables {
            useSupportLibrary = true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Debug - testing and letting others use
    signingConfigs {
        debug {
            storeFile rootProject.file("./.buildscript/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }

        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.txt"
            signingConfig signingConfigs.debug
        }
    }
}

// Retrolambda - exclude the version that the android plugin depends on
configurations {
    classpath {
        exclude group: "com.android.tools.external.lombok"
    }

    androidTestCompile {
        androidTestCompile {
            exclude group: "com.android.support"
        }
    }
}

def supportLibraryVersion = "24.2.0"
def butterKnifeVersion = "8.4.0"
def okHttpVersion = "3.4.1"
def retrofitVersion = "2.1.0"
def espressoVersion = "2.2.2"

dependencies {
    compile "com.android.support:design:${supportLibraryVersion}"
    compile "com.android.support:cardview-v7:${supportLibraryVersion}"
    compile "com.jakewharton:butterknife:${butterKnifeVersion}"
    apt "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
    compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
    compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
    compile "io.reactivex:rxandroid:1.2.1"
    compile "io.reactivex:rxjava:1.1.9"
    compile "pl.droidsonroids.gif:android-gif-drawable:1.2.0"
    compile "com.github.bumptech.glide:glide:3.7.0"
    compile "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
    compile "org.greenrobot:eventbus:3.0.0"

    androidTestCompile "junit:junit:4.12"
    androidTestCompile "org.mockito:mockito-core:1.9.5"
    androidTestCompile "com.squareup.assertj:assertj-android:1.1.1"
    androidTestCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestCompile "com.android.support.test.espresso:espresso-intents:${espressoVersion}"
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:${espressoVersion}") {
        exclude module: "appcompat"
        exclude module: "support-v4"
        exclude module: "support-v7"
        exclude module: "design"
        exclude module: "support-annotations"
        exclude module: "recyclerview-v7"
    }

    testCompile "junit:junit:4.12"
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile "com.squareup.assertj:assertj-android:1.1.1"
    testCompile "com.android.support:support-annotations:${supportLibraryVersion}"
    testCompile "com.squareup.retrofit2:retrofit-mock:${retrofitVersion}"
    testCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"

    retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:2.3.0"
}
