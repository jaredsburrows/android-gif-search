buildscript {
  rootProject.ext.ci = rootProject.hasProperty("ci")

  repositories {
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "com.gradle:build-scan-plugin:1.1.1"
    classpath "com.android.tools.build:gradle:2.2.2"
    classpath "com.novoda:gradle-android-command-plugin:1.6.0"
    classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1"
    classpath "com.vanniktech:gradle-android-apk-size-plugin:0.3.0"
    classpath "com.github.triplet.gradle:play-publisher:1.1.4"
    classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.8"
    classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
    classpath "me.tatarka:gradle-retrolambda:3.3.0"
    classpath "me.tatarka.retrolambda.projectlombok:lombok.ast:0.2.3.a2"
  }
}

repositories {
  maven { url "https://plugins.gradle.org/m2/" }
  maven { url "https://jitpack.io" }
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

apply from: rootProject.file("gradle/buildScan.gradle")
apply plugin: "com.android.application"
apply plugin: "android-command"
apply plugin: "com.github.triplet.play"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.vanniktech.android.apk.size"
apply plugin: "me.tatarka.retrolambda"
apply plugin: "net.ltgt.errorprone"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
  defaultConfig {
    applicationId "burrows.apps.example.gif"
    versionCode 1
    versionName "1.0"
    testApplicationId "burrows.apps.example.gif.test"
    testInstrumentationRunner "test.CustomTestRunner"

    resConfigs "en"

    vectorDrawables {
      useSupportLibrary true
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  // Debug - testing and letting others use
  signingConfigs {
    debug {
      storeFile rootProject.file("./.buildscript/debug.keystore")
      storePassword "android"
      keyAlias "androiddebugkey"
      keyPassword "android"
    }
  }

  buildTypes {
    debug {
      applicationIdSuffix ".debug"
      debuggable true
      testCoverageEnabled true
    }

    release {
      minifyEnabled false
      proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
      proguardFile "proguard-rules.txt"
      signingConfig signingConfigs.debug
    }
  }

  packagingOptions {
    exclude "META-INF/rxjava.properties"
  }
}

configurations {
  // Retrolambda - exclude the version that the android plugin depends on
  classpath {
    exclude group: "com.android.tools.external.lombok"
  }

  // Use "compile" configuration support version in "androidTestCompile"
  androidTestCompile {
    exclude group: "com.android.support"
    exclude group: "javax.inject"
  }

  // Use "compile" configuration support version in "testCompile"
  testCompile {
    exclude group: "com.android.support"
  }
}

def supportLibraryVersion = "25.0.0"
def butterKnifeVersion = "8.4.0"
def okHttpVersion = "3.4.1"
def retrofitVersion = "2.1.0"
def daggerVersion = "2.7"
def espressoVersion = "2.2.2"
def leakCanaryVersion = "1.5"
def junitVersion = "4.12"
def mockitoVersion = "2.2.6"
def assertjCoreVersion = "2.5.0" // Java 8 - 3.5.2
def dexMakerVersion = "1.2"

dependencies {
  compile "com.android.support:design:${supportLibraryVersion}"
  compile "com.android.support:cardview-v7:${supportLibraryVersion}"
  compile "com.jakewharton:butterknife:${butterKnifeVersion}"
  compile "com.squareup.okhttp3:logging-interceptor:${okHttpVersion}"
  compile "com.squareup.okhttp3:okhttp:${okHttpVersion}"
  compile "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0-RC3"
  compile "com.squareup.retrofit2:converter-gson:${retrofitVersion}"
  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "io.reactivex.rxjava2:rxandroid:2.0.0-RC1"
  compile "io.reactivex.rxjava2:rxjava:2.0.0-RC4"
  compile "pl.droidsonroids.gif:android-gif-drawable:1.2.1"
  compile "com.github.bumptech.glide:glide:3.7.0"
  compile "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"
  compile "com.google.dagger:dagger:${daggerVersion}"

  debugCompile "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
  releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"

  annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
  annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

  androidTestCompile "junit:junit:${junitVersion}"
  androidTestCompile "org.mockito:mockito-core:${mockitoVersion}"
  androidTestCompile "org.assertj:assertj-core:${assertjCoreVersion}"
  androidTestCompile "com.android.support.test:runner:0.5"
  androidTestCompile "com.android.support.test.espresso:espresso-core:${espressoVersion}"
  androidTestCompile "com.android.support.test.espresso:espresso-intents:${espressoVersion}"
  androidTestCompile "com.android.support.test.espresso:espresso-contrib:${espressoVersion}"
  androidTestCompile "com.google.dexmaker:dexmaker:${dexMakerVersion}"
  androidTestCompile "com.google.dexmaker:dexmaker-mockito:${dexMakerVersion}"
  androidTestCompile "com.google.dagger:dagger:${daggerVersion}"
  androidTestCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"

  androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

  testCompile "junit:junit:${junitVersion}"
  testCompile "org.mockito:mockito-core:${mockitoVersion}"
  testCompile "org.assertj:assertj-core:${assertjCoreVersion}"
  testCompile "com.squareup.okhttp3:mockwebserver:${okHttpVersion}"
  testCompile "org.robolectric:shadows-support-v4:3.1.2"
  testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"
  testCompile "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"

  retrolambdaConfig "net.orfjackal.retrolambda:retrolambda:2.3.0"
}
